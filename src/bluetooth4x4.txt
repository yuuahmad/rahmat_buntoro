#include <Arduino.h>
#include <Keypad.h>
#include <Wire.h>
#include <BLEDevice.h>
#include <BleKeyboard.h>
#include <RotaryEncoder.h>

const int pin1 = 2;
const int pin2 = 4;
RotaryEncoder encoder(pin1, pin2, RotaryEncoder::LatchMode::TWO03);

const byte ROWS = 4; //four rows
const byte COLS = 4; //four columns

char keys[ROWS][COLS] = {
    {'1', '2', '3', 'A'},
    {'4', '5', '6', 'B'},
    {'7', '8', '9', 'C'},
    {'*', '0', '#', 'D'},
};

byte rowPins[ROWS] = {13, 12, 14, 27}; //connect to the row pinouts of the keypad
byte colPins[COLS] = {26, 25, 33, 32}; //connect to the column pinouts of the keypad

Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

// First param is name
// Second is manufacturer
// Third is initial batter level
BleKeyboard bleKeyboard("BLE_Switcheroonie", "BLough", 100);

void setup()
{
  Serial.begin(115200);
  bleKeyboard.begin();
}

// This will hold down all the following buttons.
void sendMacroCommand(uint8_t key)
{
  bleKeyboard.press(KEY_LEFT_CTRL);
  bleKeyboard.press(KEY_LEFT_SHIFT);
  bleKeyboard.press(KEY_LEFT_ALT);
  bleKeyboard.press(key);
}

void loop()
{
  static int pos = 0;
  encoder.tick();

  int newPos = encoder.getPosition();
  if (pos != newPos)
  {
    Serial.print(newPos);
    Serial.print(" ");
    pos = newPos;
  } // if

  char key = keypad.getKey();
  if (bleKeyboard.isConnected() && key)
  {
    Serial.println(key);
    switch (key)
    {
    case '1':
      bleKeyboard.print("hai dunia");
      break;
    case '2':
      bleKeyboard.write(KEY_MEDIA_PLAY_PAUSE);
      break;
    case '3':
      bleKeyboard.write(KEY_MEDIA_CALCULATOR);
      break;
    case '4':
      sendMacroCommand(KEY_F4);
      break;
    case '5':
      sendMacroCommand(KEY_F5);
      break;
    case '6':
      sendMacroCommand(KEY_F6);
      break;
    case '7':
      sendMacroCommand(KEY_F7);
      break;
    case '8':
      sendMacroCommand(KEY_F8);
      break;
    case '9':
      sendMacroCommand(KEY_F9);
      break;
    case '0':
      sendMacroCommand(KEY_F10);
      break;
    case '*':
      sendMacroCommand(KEY_F11);
      break;
    case '#':
      sendMacroCommand(KEY_F12);
      break;
    case 'A':
      sendMacroCommand('a');
      break;
    case 'B':
      sendMacroCommand('b');
      break;
    case 'C':
      sendMacroCommand('c');
      break;
    case 'D':
      sendMacroCommand('d');
      break;
    }

    delay(100);
    bleKeyboard.releaseAll(); // this releases the buttons
  }
}